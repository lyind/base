/*
 * Copyright (C) 2017  Jonas Zeiger <jonas.zeiger@talpidae.net>
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
    id 'idea'
    id 'java'
    id 'maven'
}

group 'net.talpidae'
version '2'

sourceCompatibility = JavaVersion.VERSION_1_10
targetCompatibility = JavaVersion.VERSION_1_10
[javadoc, compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

dependencies {
    annotationProcessor "org.projectlombok:lombok:1.18.10"
    compileOnly "org.projectlombok:lombok:1.18.10"
    testAnnotationProcessor "org.projectlombok:lombok:1.18.10"
    testCompileOnly "org.projectlombok:lombok:1.18.10"

    // RESTEasy
    compile('org.jboss.resteasy:resteasy-jaxrs:4.0.0.Beta5') {
        // enforce use of JCL over SLF4J
        exclude group: 'commons-logging'
    }
    compile 'org.jboss.resteasy:resteasy-servlet-initializer:4.0.0.Beta5'
    compile 'org.jboss.resteasy:resteasy-client:4.0.0.Beta5'
    compile 'org.jboss.resteasy:resteasy-jaxb-provider:4.0.0.Beta5'
    compile 'org.jboss.resteasy:resteasy-jackson2-provider:4.0.0.Beta5'
    compile 'org.jboss.resteasy:resteasy-rxjava2:4.0.0.Beta5'

    // Support guice-4.2.0 with Java 9/10/11
    compile('org.jboss.resteasy:resteasy-guice:4.0.0.Beta5') {
        exclude group: 'com.google.inject'
    }
    compile 'org.ow2.asm:asm:7.3.1'
    compile 'cglib:cglib:3.3.0'
    compile 'com.google.inject:guice:4.2.2:classes'

    // for class-path scanning (auto-register JAX-RS resources)
    compile 'org.reflections:reflections:0.9.12'

    compile('com.google.inject.extensions:guice-multibindings:4.2.2') {
        exclude group: 'com.google.inject'
    }

    compile 'net.sf.jopt-simple:jopt-simple:6.0-alpha-3'

    compile 'ch.qos.logback:logback-core:1.3.0-alpha5'
    compile 'ch.qos.logback:logback-classic:1.3.0-alpha5'
    compile 'org.slf4j:slf4j-api:1.8.0-beta2'
    compile 'org.slf4j:jul-to-slf4j:1.8.0-beta2'
    compile 'org.slf4j:log4j-over-slf4j:1.8.0-beta2'
    compile 'org.slf4j:jcl-over-slf4j:1.8.0-beta2'

    compile 'io.jsonwebtoken:jjwt:0.9.1'

    compile 'io.undertow:undertow-websockets-jsr:2.0.29.Final'

    compile 'com.fasterxml.jackson.core:jackson-core:2.9.6'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.9.6'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.6'
    compile 'com.fasterxml.jackson.module:jackson-module-afterburner:2.9.6'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.9.6'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.6'

    runtime 'javax.xml.bind:jaxb-api:2.3.1'

    compile 'org.flywaydb:flyway-core:6.1.4'
    compile 'com.zaxxer:HikariCP:3.4.2'

    compile 'org.jdbi:jdbi3-core:3.12.0'
    compile 'org.jdbi:jdbi3-sqlobject:3.12.0'

    compile 'net.ttddyy:datasource-proxy:1.5.1'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}


if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addBooleanOption('html5', true)
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
